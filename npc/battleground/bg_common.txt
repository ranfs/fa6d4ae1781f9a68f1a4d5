// ==============================================================================
// BattleGround System - Common NPCs
// ==============================================================================

// MapFlags
// *********************************************************************

bat_room	mapflag	nomemo
bat_room	mapflag	nowarpto
bat_room	mapflag	nobranch
bat_room	mapflag	nopenalty
bat_room	mapflag	noteleport
bat_room	mapflag	nosave	SavePoint
bat_room	mapflag	allow_bg_items
// Mapflag Town is required to Join BG Queues
bat_room	mapflag	town

// Battleground Queue Admin
// *********************************************************************
-	script	BG_Queue_Join	-1,{
	end;

OnInit:
	// Configuration
	// *****************************************************
	// Battleground Rotation - Main NPC name
	setarray .BG_Arenas$[0],	     "Rush";
	// Battleground Arena Name - for announcements
	setarray .BG_Names$[0],		"Rush";
	// Minimum Players to start the BG
	setarray .BG_Min[0],	                         1;
	// Maximum Players per Team
	setarray .BG_Max[0],	                        50;
	// BG Message Colors
	setarray .BG_Color$[0],	                "0xA0522D",    "0x4169E1",	"0x808000";
	// Team Building Mode : 0 = Lineal | 1 = Random | 2 = Class Priority
	set .TeamMode, 0;
	// Main Code
	// *****************************************************
	set .BG_Count, getarraysize(.BG_Arenas$);
	set .BG_Queue, bg_queue_create("Battleground Arena","BG_Queue_Join::OnJoinEvent",80);
	set .VS_Queue, bg_queue_create("Free For All Arena","BG_Queue_Join::OnVersusJoin",80);
	// Move to Next Arena
	if( $BG_Index >= .BG_Count ) set $BG_Index,0; // Restart Rotation
	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	set .Ready, 1;
	donpcevent "BG_Queue_Join::OnDoHappyHour";
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method
	end;

OnRotate:
OnTimer120000: // Rotation if BG don't start... Comment if you don't want to use this method. 120 = 2 Minutes
	set $@BG_Status, 0;
	// Warps Teams
	bg_warp $@BG_Team1,"bat_room",155,150;
	bg_warp $@BG_Team2,"bat_room",155,150;
	bg_warp $@BG_Team3,"bat_room",155,150;
	// Destroy Teams
	bg_destroy $@BG_Team1; set $@BG_Team1, 0;
	bg_destroy $@BG_Team2; set $@BG_Team2, 0;
	bg_destroy $@BG_Team3; set $@BG_Team3, 0;

	// Move to Next Arena
	if( set($BG_Index,$BG_Index + 1) >= .BG_Count )
		set $BG_Index,0; // Restart Rotation

	set .BG_Arena$,.BG_Arenas$[$BG_Index];
	initnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

	// Trigger the Event - Maybe people is waiting...
OnJoinEvent:
	if( $@BG_Status == 2 )
		end; // BG Ending... Must wait until OnRotate is called

	set .@Queue_Count, bg_queue_data(.BG_Queue,0);
	switch( $@BG_Status )
	{
	case 0: // Idle - Ready to Start
		if( .BG_Arena$ == "Tierra_TI" )
			set .@Req, .BG_Min[$BG_Index] * 3;
		else
			set .@Req, .BG_Min[$BG_Index] * 2;

		if( .@Queue_Count >= .@Req )
		{
			donpcevent .BG_Arena$ + "::OnBuildTeams";

			// Fill Teams with Queue Members			
			if( .BG_Arenas$[$BG_Index] == "Tierra_TI" )
				bg_queue2teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2,$@BG_Team3;
			else
				bg_queue2teams .BG_Queue,.BG_Max[$BG_Index],.TeamMode,$@BG_Team1,$@BG_Team2;

			stopnpctimer; // Rotation if BG don't start... Comment if you don't want to use this method

			set $@BG_Status,1;
			set .@msg$, "Battleground -- " + .BG_Names$[$BG_Index] + " -- Begining";
			announce .@msg$,0,.BG_Color$[$BG_Index];

			donpcevent .BG_Arena$ + "::OnReady";
		}
		else
		{
			set .@msg$, "Batalha Campal -- " + .BG_Names$[$BG_Index] + " -- faltam " + (.@Req - .@Queue_Count) + " jogadores para o seu início.";
			announce .@msg$,0,.BG_Color$[$BG_Index];
		}
		break;
	case 1: // Running - Others can join
		if( .@Queue_Count > 0 )
		{
			if( .BG_Arena$ == "Tierra_TI" )
			{
				bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],$@BG_Team1,$@BG_Team2,$@BG_Team3;
				set .@BG_Count1, bg_get_data($@BG_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_Team2,0);
				set .@BG_Count3, bg_get_data($@BG_Team3,0);
				set .@msg$, "Batalha Campal -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + ", G: " + .@BG_Count3 + "/" + .BG_Max[$BG_Index] + " (Em andamento)";
			}
			else
			{
				bg_balance_teams .BG_Queue,.BG_Max[$BG_Index],$@BG_Team1,$@BG_Team2;
				set .@BG_Count1, bg_get_data($@BG_Team1,0);
				set .@BG_Count2, bg_get_data($@BG_Team2,0);
				set .@msg2$, "Batalha Campal -- " + .BG_Names$[$BG_Index] + " -- B: " + .@BG_Count1 + "/" + .BG_Max[$BG_Index] + ", R: " + .@BG_Count2 + "/" + .BG_Max[$BG_Index] + " (Em andamento)";
			}

			announce .@msg$,0,.BG_Color$[$BG_Index];
			mapannounce "bat_room",.@msg2$,0,.BG_Color$[$BG_Index];
		}
		break;
	}
	end;

OnVersusJoin:
	if( $@VS_Status != 0 )
		end;

	set .@Queue_Count, bg_queue_data(.VS_Queue,0);
	if( .@Queue_Count >= 10 )
	{
		donpcevent "FFA_Arena::OnBuildTeams";
		bg_queue2teams .VS_Queue,1,0,$@VS_Team[0],$@VS_Team[1],$@VS_Team[2],$@VS_Team[3],$@VS_Team[4],$@VS_Team[5],$@VS_Team[6],$@VS_Team[7],$@VS_Team[8],$@VS_Team[9];
		set $@VS_Status,1;
		announce "Battleground -- Free For All -- Begining",0,0x483D8B;
		donpcevent "FFA_Arena::OnReady";
	}
	else
		announce "Battleground -- Free For All -- " + (10 - .@Queue_Count) + " more players to start",0,0x483D8B;
	end;

OnFri0600: // Friday, 6 a.m.
	rankreset 1; // Ranking Reset
	end;
}

// Registration NPC
// *********************************************************************
-	script	BG_Register	-1,{

	if( getvariableofnpc(.Ready,"BG_Queue_Join") == 0 )
		donpcevent "BG_Queue_Join::OnInit";

	mes "[^FFA500Batalha Campal^000000]";
	mes "Bem Vindo ao novo sistema da Batalha Campal ragnaYOKAI!";
	mes "Aqui você pode registrar-se ou registrar seu Grupo no sistema.";
	mes "O sistema seleciona os Times e Arena randomicamente e segue alternando as Arenas de forma consecutiva.";
	mes "Conquest --> Rush --> Conquest --> Conquest e por ai vai.";
	mes "Oque deseja fazer?";
	next;
		switch( select("Registrar:Sair do Sistema:Informações das Arenas") )
		{
		case 1:
			if( BaseLevel < 80 )
			{
				mes "[^FFA500Batalha Campal^000000]";
				mes "Level minimo requerido para participar da BG é 80.";
				close;
			}

			mes "[^FFA500Batalha Campal^000000]";
			mes "Registrado com SUCESSO!";
			mes "Aguarde alguns segundos no bat_room até ser recallado para a Arena.";
			close2;
			bg_queue_join getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 2:
			mes "[^FFA500Batalha Campal^000000]";
			mes "Você está registrado, deseja remover seu registro?.";
			next;
			if( select("Sim, remover registro.:Não, vou continuar...") == 2 )
			{
				mes "[^FFA500Batalha Campal^000000]";
				mes "Ok, sem problemas.";
				close;
			}

			mes "[^FFA500Batalha Campal^000000]";
			mes "Registro removido com SUCESSO!";
			close2;
			bg_queue_leave getvariableofnpc(.BG_Queue,"BG_Queue_Join");
			end;
		case 3:
			mes "[^FFA500Batalha Campal^000000]";
			mes "Qual modalidade você deseja saber mais?";
			next;
			switch(select("Conquest:Rush") ) {
			case 1:
				mes "[^FFA500Batalha Campal^000000]";
				mes "Se você estiver atacando deve destruir as defesas inimigas e a Emperium.";
				next;
				mes "[^FFA500Batalha Campal^000000]";
				mes "Se você estiver defendendo deve proteger as defesas do castelo e a Emperium.";
				close;
			case 2:
				mes "[^FFA500Batalha Campal^000000]";
				mes "Lute para conquistar o Castelo e o defenda ate o fim do tempo.";
				next;
				mes "[^FFA500Batalha Campal^000000]";
				mes "Se falhar na primeira tentativa de conquista, derrote a defesa inimiga e conquiste o Castelo.";
				close;				
			}
	}
}

bat_room,154,154,4	duplicate(BG_Register)	Batalha Campal#bat	728
// prontera,155,180,4	duplicate(BG_Register)	Battle Recruiter#prt	728
payold,100,129,3	script	Batalha Campal#prt	728,{
	mes "[^FFA500Batalha Campal^000000]";
	mes "Bem Vindo ao novo sistema da Batalha Campal ragnaYOKAI!";
	mes "Aqui você pode registrar-se ou registrar seu Grupo no sistema.";
	mes "O sistema seleciona os Times e Arena randomicamente e segue alternando as Arenas de forma consecutiva.";
	mes "Conquest --> Rush --> Conquest --> Conquest e por ai vai.";
	mes "Oque deseja fazer?";
	next;
		if( select("Ir ao Bat Romm:Cancelar")  == 2 ) close;
			mes "[^FFA500Batalha Campal^000000]";
			mes "Vamos!";
			close2;
			warp "bat_room",154,150;
			end;
OnInit:
waitingroom "Batalha Campal",0; 
end;
}
// Flags
// *********************************************************************

-	script	Guillaume camp#bat	973,{ end; }
-	script	Croix camp#bat	974,{ end; }

// BattleGround Warper - Exit
// *********************************************************************

bat_room,148,150,4	script	Teletransportador#bat	124,{
	mes "[Teletransportador]";
	mes "Olá "+strcharinfo(0)+".";
	mes "Você gostaria de deixar o campo de batalha? Use meu serviço para voltar para a sua Cidade.";
	next;
	if( select("Deixar:Não Obrigado.") == 2 ) {
		mes "[Teletransportador]";
		mes "Estarei sempre aqui caso você estiver na necessidade de utilizar meu serviço.";
		close;
	}

	switch( @ArenaPVP_out )
	{
		// Rune Midgard Republic
		case 1: warp "prontera",149,92; break;
		case 2: warp "payon",165,98; break;
		case 3: warp "morocc",153,94; break;
		case 4: warp "umbala",121,143; break;
		case 5: warp "comodo",196,140; break;
		case 6: warp "niflheim",214,193; break;
		case 7: warp "aldebaran",143,111; break;
		case 8: warp "geffen",107,53; break;
		// Schwarzard Republic
		case 9: warp "yuno",151,177; break;
		case 10: warp "hugel",99,143; break;
		case 11: warp "lighthalzen",167,93; break;
		case 12: warp "einbroch",70,194; break;
		case 13: warp "einbech",168,130; break;
		// Arunafelz Republic
		case 14: warp "rachel",118,114; break;
		case 15: warp "veins",207,122; break;
		// Islands
		case 16: warp "nameless_n",161,179; break;
		case 17: warp "louyang",213,106; break;
		case 18: warp "gonryun",154,111; break;
		case 19: warp "ayothaya",148,109; break;
		case 20: warp "moscovia",229,195; break;
		case 21: warp "xmas",151,127; break;
		case 22: warp "amatsu",203,107; break;
		case 23: warp "izlude",126,114; break;
		case 24: warp "brasilis",195,211; break;
		case 25: warp "manuk",279,214; break;
		case 26: warp "splendide",200,174; break;
		// Anyplace else
		default:
			set .@spoint$, getsavepoint(0);
			set .@x, getsavepoint(1);
			set .@y, getsavepoint(2);
			mes "[Teleporter]";
			mes "You will be sent back to " + .@spoint$ + ".";
			close2;
			warp .@spoint$, .@x, .@y;
			break;
	}
	
	set @ArenaPVP_out, 0;
	end;
}

// Kafra
// *********************************************************************
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}
